// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.buildConfig = [
            compileSdk   : 28, // SDK 编译版本
            minSdk       : 19, // 最小 SDK 版本
            instantMinSdk: 21, // Instant Run 的最小 SDK 版本
            targetSdk    : 28, // 目标 SDK 版本

            version      : [
                    major: 1, // 主版本号
                    minor: 4, // 子版本号
                    patch: 0, // 修正版本号
            ],
    ]

    // 版本名采用 GNU 风格，主版本号.子版本号.修正版本号，例如 1.2.10
    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    // 版本号由版本名映射，主版本号 * 10000 + 子版本号 * 100 + 修正版本号，例如 1.2.10 -> 10210
    ext.buildConfig.version['code'] = buildConfig.version.major * 10000 + buildConfig.version.minor * 100 + buildConfig.version.patch

    ext.versions = [
            /* Plugin. */
            plugin    : [
                    android    : '3.5.0',
                    ktlint     : '7.2.1',
            ],
            /* AndroidX. */
            androidx  : [
                    test: [
                            coreKtx     : '1.2.1-alpha02',
                            runner      : '1.3.0-alpha02',
                            rules       : '1.3.0-alpha02',
                            junitKtx    : '1.1.2-alpha02',
                            truth       : '1.3.0-alpha02',
                            orchestrator: "1.3.0-alpha02",
                    ],
            ],
            /* Kotlin. */
            kotlin    : [
                    stdlib: '1.3.41',
            ],
            test      : [
                    mockk      : '1.9.3',
                    robolectric: '4.3',
            ],
            appcompat : '1.1.0-rc01',
            leakcanary: '2.0-alpha-1',
    ]

    ext.mavenUrl = [
            aliyun : 'http://maven.aliyun.com/nexus/content/groups/public/',
            jitpack: 'https://jitpack.io',
            kotlinx: 'https://kotlin.bintray.com/kotlinx'
    ]

    ext.artifactory = [
            groupId   : 'xzy-tools',
            artifactId: 'keyboard',
    ]

    ext.deps = [
            plugin    : [
                    android    : "com.android.tools.build:gradle:${versions.plugin.android}",
                    kotlin     : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.stdlib}",
                    ktlint     : "org.jlleitschuh.gradle:ktlint-gradle:${versions.plugin.ktlint}",
            ],
            kotlin    : [
                    stdlib: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin.stdlib}",
                    junit : "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin.stdlib}",
            ],
            androidx  : [
                    test: [
                            coreKtx     : "androidx.test:core-ktx:${versions.androidx.test.coreKtx}",
                            runner      : "androidx.test:runner:${versions.androidx.test.runner}",
                            rules       : "androidx.test:rules:${versions.androidx.test.rules}",
                            junitKtx    : "androidx.test.ext:junit-ktx:${versions.androidx.test.junitKtx}",
                            truth       : "androidx.test.ext:truth:${versions.androidx.test.truth}",
                            orchestrator: "androidx.test:orchestrator:${versions.androidx.test.orchestrator}",
                    ],
            ],
            test      : [
                    mockk      : "io.mockk:mockk:${versions.test.mockk}",
                    robolectric: "org.robolectric:robolectric:${versions.test.robolectric}",
            ],
            appcompat : "androidx.appcompat:appcompat:${versions.appcompat}",
            leakcanary: "com.squareup.leakcanary:leakcanary-android:${versions.leakcanary}",
    ]

    repositories {
        maven { url mavenUrl.aliyun }
        maven { url mavenUrl.kotlinx }
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath deps.plugin.android
        classpath deps.plugin.kotlin
        classpath deps.plugin.ktlint
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    ktlint {
        android = true
    }

    repositories {
        maven { url mavenUrl.aliyun }
        maven { url mavenUrl.kotlinx }
        google()
        jcenter()
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all Kotlin stdlib artifacts to use the same version.
                if (details.requested.group == 'org.jetbrains.kotlin'
                        && details.requested.name.startsWith('kotlin-stdlib')) {
                    details.useVersion versions.kotlin.stdlib
                }
            }
        }
    }
}